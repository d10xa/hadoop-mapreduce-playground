import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'java'
    id "de.undercouch.download" version "3.0.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.apache.hadoop:hadoop-common:$hadoopVersion"
    compile "org.apache.hadoop:hadoop-mapreduce-client-core:$hadoopVersion"
    compile "org.apache.hadoop:hadoop-mapreduce-client-jobclient:$hadoopVersion"
}

task downloadHadoop(type: Download, group: 'hadoop') {
    src "http://apache-mirror.rbc.ru/pub/apache/hadoop/common/hadoop-$hadoopVersion/hadoop-${hadoopVersion}.tar.gz"
    dest "$projectDir/.hadoop/hadoop-${hadoopVersion}.tar.gz"
}

task untarHadoop(type: Copy, group: 'hadoop') {
    from tarTree(resources.gzip("$projectDir/.hadoop/hadoop-${hadoopVersion}.tar.gz"))
    into "$projectDir/.hadoop/"
}

task wordCount(type: Exec, group: 'hadoop') {
    environment JAVA_HOME: System.getProperty("java.home")
    commandLine "$projectDir/.hadoop/hadoop-${hadoopVersion}/bin/hadoop",
            'jar',
            "$buildDir/libs/hadoop-mapreduce-playground.jar",
            "ru.d10xa.hadoop.WordCount",
            "input",
            "output"
}

wordCount.dependsOn jar
wordCount.outputs.files file("$projectDir/output")
wordCount.dependsOn cleanWordCount
